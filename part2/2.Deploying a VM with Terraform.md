# Terraform Overview and Azure VM Deployment Guide

This guide is designed to walk you through the basics of Terraform, a tool for Infrastructure as Code (IaC), and help you deploy your first VM on Azure. 

## What is Terraform?

Terraform, created by HashiCorp, is an open-source IaC tool that allows you to define and provision infrastructure using a high-level configuration language. 

One of the coolest things about Terraform is its ability to work across multiple cloud providers. Whether you're using AWS, Azure, Google Cloud, or even on-premises solutions, Terraform provides a consistent way to manage your infrastructure. This means you can use the same tool and similar configurations to deploy resources across different environments, making multi-cloud strategies much simpler.

Terraform operates on a principle called "infrastructure as code," or IaC, which means you write code to define your infrastructure. This has some great benefits. For one, your infrastructure setup becomes version-controlled, just like your application code. You can track changes, roll back to previous versions, and collaborate with your team more effectively.

It also uses a feature called "state files" to keep track of your infrastructure's *current* state. When you run a Terraform `plan`, it compares your desired state (defined in your configuration files) with the actual state (recorded in the state file) and shows you what changes it will make. This gives you a clear understanding of what will happen before you apply any changes, reducing the risk of unexpected issues.

Another fantastic feature of Terraform is its modularity. You can write reusable modules for common infrastructure patterns. For example, if you frequently set up a particular type of server cluster or database configuration, you can create a module for it. This makes your code more organized and easier to manage, and it saves time since you don't have to write the same configuration over and over again.

Terraform also excels in automation. By integrating with continuous integration/continuous deployment (CI/CD) pipelines, you can automate your infrastructure deployments, ensuring that they are consistent, repeatable, and reliable. This is particularly useful for scaling and managing large infrastructures, where manual configuration would be impractical and error-prone.

Teams can work together on the same infrastructure code, leveraging tools like Git to handle version control. Terraform Cloud and Terraform Enterprise provide additional collaboration features, such as remote state management, team access controls, and detailed policy enforcement, making it easier for large teams to work together securely.

However, like any tool, Terraform has its learning curve. Understanding HCL and the various providers' specifics can take some time, especially if you're new to IaC concepts. But once you get the hang of it, you'll find that the benefits far outweigh the initial investment in learning!

## Key Concepts

- **Providers** 

Think of providers as the bridge between Terraform and the various services you want to manage. Providers are responsible for understanding API interactions with specific platforms or services. For instance, if you're deploying resources on AWS, Azure, Google Cloud, or even managing GitHub repositories, you'll use a provider specific to that platform.

Providers are like plugins that enable Terraform to communicate with different cloud services. When you configure a provider in your Terraform script, you're essentially telling Terraform which cloud or service you're working with and how to authenticate with it. For example, to use AWS, you might configure the AWS provider with your access keys. This setup allows Terraform to create, update, or delete resources on AWS on your behalf.

- **Resources** 

Resources are the fundamental building blocks in Terraform. They represent the various components of your infrastructure, like virtual machines, databases, networks, and so on. Each resource is defined using a specific syntax in your Terraform configuration files.

When you define a resource, you specify its type and the configuration settings required to create that resource. For example, an AWS EC2 instance, an Azure Virtual Machine, or a Google Cloud Storage bucket are all examples of resources. You can also define dependencies between resources to ensure they are created in the correct order.

Resources are what you manage and manipulate with Terraform. You can add, modify, or remove resources by changing your configuration files and applying those changes with Terraform commands. This approach ensures your infrastructure is defined in a clear, declarative way, making it easier to understand and manage.


- **State** 

State is like Terraform's memory. It keeps track of the current state of your infrastructure as defined by your Terraform configurations. When you run terraform apply, Terraform compares your configuration (the desired state) with the actual state of your infrastructure (recorded in the state file) and determines what changes are needed to align the actual state with the desired state.

The state file is crucial because it stores information about the resources Terraform manages, including IDs and other metadata. This information allows Terraform to know exactly which resources it needs to create, update, or delete. The state file also helps Terraform understand dependencies between resources and manage them efficiently.

Terraform's state can be stored locally or remotely. For collaboration and team environments, remote state storage is recommended. This ensures that everyone on the team is working with the same state, avoiding conflicts and inconsistencies. Terraform Cloud and Terraform Enterprise offer remote state management with additional features like locking and versioning to further enhance collaboration.

## Terraform commands

- Plan

Think of it as a dry run or a rehearsal.

When you run `terraform plan`, Terraform goes through your configuration files and generates an execution plan. This plan shows you exactly what actions Terraform will take to reach the desired state of your infrastructure. It might show that it will add new resources, modify existing ones, or even destroy some that are no longer needed.

- Apply

Once you’re happy with the plan and you’re confident that the changes are correct, you move on to the `terraform apply` command. This is where the magic happens. When you run terraform apply, Terraform takes the plan it generated and executes it, making all the necessary changes to your infrastructure.

One of the great things about terraform apply is that it provides real-time feedback. You can watch as Terraform creates and modifies resources, and it will notify you if any issues arise during the process. Once it’s done, you’ll have a detailed log of what was created, updated, or deleted.

- Destroy

Now, what if you need to tear down your infrastructure? Maybe you’re done with a project or you need to clean up resources to avoid unnecessary costs. That’s where `terraform destroy` comes in. This command is the opposite of apply. It’s like hitting the reset button.

When you run terraform destroy, Terraform will go through your configuration files and the state file to determine which resources need to be deleted. It then prompts you to confirm that you really want to destroy these resources. This confirmation step is crucial to avoid accidental deletions.

---

## Getting Started with Terraform

1. Download and install Terraform from [terraform.io](https://developer.hashicorp.com/terraform/install?product_intent=terraform).
2. If you don't have an Azure account, sign up at [azure.microsoft.com](azure.microsoft.com).
3. Install Azure CLI from [learn.microsoft.com](https://learn.microsoft.com/en-us/cli/azure/install-azure-cli).


## Setup

1. Use Azure CLI to login.

```bash
az login  --tenant [ask your tutor for tenant id] --use-device-code
```

Follow the instructions to log into your Azure account in a browser

2. Create a new directory for your Terraform files.

```bash
mkdir terraform-azure-vm
cd terraform-azure-vm
```

## Basic Terraform Configuration

Create a file named `main.tf`. 

> :exclamation: Be sure to change the default values for the variables `team_name` and `user_id`!

```hcl
# Variables
variable "team_name" {
  description = "Name of the team"
  type        = string
  default     = "myteam"
}

variable "user_id" {
  description = "User ID"
  type        = string
  default     = "user123"
}

# Provider
provider "azurerm" {
  features {}
}

# Create a resource group if it doesn't exist
resource "azurerm_resource_group" "myterraformgroup" {
  name     = "rg-academytf-${var.team_name}-${var.user_id}"
  location = "uksouth"

  tags = {
    project    = "academy"
    team       = var.team_name
    user       = var.user_id
  }
}

# Create virtual network
resource "azurerm_virtual_network" "myterraformnetwork" {
  name                = "vnet-academytf-${var.team_name}-${var.user_id}"
  address_space       = ["10.0.0.0/16"]
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

# Create subnet
resource "azurerm_subnet" "myterraformsubnet" {
  name                 = "snet-academytf-${var.team_name}-${var.user_id}"
  resource_group_name  = azurerm_resource_group.myterraformgroup.name
  virtual_network_name = azurerm_virtual_network.myterraformnetwork.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Create public IPs
resource "azurerm_public_ip" "myterraformpublicip" {
  name                = "pip-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name
  allocation_method   = "Dynamic"

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

# Create Network Security Group and rule
resource "azurerm_network_security_group" "myterraformnsg" {
  name                = "nsg-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
  security_rule {
    name                       = "HTTP"
    priority                   = 1002
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "80"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

# Create network interface
resource "azurerm_network_interface" "myterraformnic" {
  name                = "nic-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  ip_configuration {
    name                          = "myNicConfiguration"
    subnet_id                     = azurerm_subnet.myterraformsubnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.myterraformpublicip.id
  }

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

# Connect the security group to the network interface
resource "azurerm_network_interface_security_group_association" "example" {
  network_interface_id      = azurerm_network_interface.myterraformnic.id
  network_security_group_id = azurerm_network_security_group.myterraformnsg.id
}

# Create virtual machine
resource "azurerm_linux_virtual_machine" "myterraformvm" {
  name                  = "vm-academytf-${var.team_name}-${var.user_id}"
  location              = "uksouth"
  resource_group_name   = azurerm_resource_group.myterraformgroup.name
  network_interface_ids = [azurerm_network_interface.myterraformnic.id]
  size                  = "Standard_B1ls"

  os_disk {
    name                 = "myOsDisk"
    caching              = "ReadWrite"
    storage_account_type = "StandardSSD_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "18.04-LTS"
    version   = "latest"
  }

  computer_name                   = "${var.user_id}vm"
  admin_username                  = "azureuser"
  disable_password_authentication = true

  admin_ssh_key {
    username   = "azureuser"
    public_key = file("~/.ssh/id_rsa.pub")
  }

  boot_diagnostics {}

  custom_data = base64encode(<<-EOT
    #!/usr/bin/env bash
    sudo apt-get update -y
    sudo apt-get install nginx -y
    sudo systemctl restart nginx
  EOT
  )

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

# Dynamically allocated IP so we need to look this up after
data "azurerm_public_ip" "public_ip" {
  name                = "pip-academytf-${var.team_name}-${var.user_id}"
  resource_group_name = azurerm_resource_group.myterraformgroup.name
  depends_on          = [azurerm_public_ip.myterraformpublicip, azurerm_linux_virtual_machine.myterraformvm]
}

# Output the Public IP
output "azurerm_public_ip" {
  value       = data.azurerm_public_ip.public_ip.ip_address
  description = "Public IP associated with the VM instance"
}


```

## Initialize your directory to download the necessary plugins.

When we start working with our Terraform project we need to first initialise it. 

*"The terraform init command is used to initialize a working directory containing Terraform configuration files. This is the first command that should be run after writing a new Terraform configuration or cloning an existing one from version control. It is safe to run this command multiple times."*

```bash
terraform init
```

#### Example Output:
```bash
Initializing the backend...

Initializing provider plugins...
- Finding hashicorp/azurerm versions matching "~> 2.0"...
- Installing hashicorp/azurerm v2.49.0...
- Installed hashicorp/azurerm v2.49.0 (signed by HashiCorp)

Terraform has created a lock file .terraform.lock.hcl to record the provider
selections it made above. Include this file in your version control repository
so that Terraform can guarantee to make the same selections by default when
you run "terraform init" in the future.

Terraform has been successfully initialized!

You may now begin working with Terraform. Try running "terraform plan" to see
any changes that are required for your infrastructure. All Terraform commands
should now work.

If you ever set or change modules or backend configuration for Terraform,
rerun this command to reinitialize your working directory. If you forget, other
commands will detect it and remind you to do so if necessary.
```

## Generate an execution plan to see what Terraform will do.

The terraform plan command is used to create an execution plan. Terraform performs a refresh, and determines what actions it needs to take to achieve the desired state specified in the configuration files.

This command is a convenient way to check whether the execution plan for a set of changes matches your expectations without making any changes to real resources or to the state. For example, terraform plan might be run before committing a change to version control, to create confidence that it will behave as expected.

```bash
terraform plan
```

Study the output of the plan. It will show you the Resources that Terraform intends to update, create and destroy. It will also show the properties it intends to set. Notice that some properties are not available at plan stage, these will only be determined when the plan is executed.

## Apply the plan to create the resources.

The terraform apply command is used to apply the changes required to reach the desired state of the configuration.

```bash
terraform apply
```

Terraform will ask if you are sure you wish to apply the changes. Enter Yes to aceept and start the execution.

```bash
Do you want to perform these actions?
  Terraform will perform the actions described above.
  Only 'yes' will be accepted to approve.

  Enter a value: yes
  ```

If all goes well terraform will create the resources and output the Public Ip address for your newly created VM.

```bash
  Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

azurerm_public_ip = "xx.xxx.xxx.xxx"
```
---

## Looking in the Portal

We can look at the resources created in the portal (look for the resource group that matches your team and name): https://portal.azure.com/?quickstart=True#blade/HubsExtension/BrowseResourceGroups

## Accessing your VM

We can access our VM using the public IP Address. In a browser navigate the IP address output by your terraform: http://\<your IP Address>

You should see the Nginx Welcome Page.

Another thing we can do is get remote shell access to the machine. To do this we use a utility (and protocol) called [SSH](https://en.wikipedia.org/wiki/SSH_(Secure_Shell))

```bash
# Create an SSH connection to VM (don't include the angle brackets! <>)
ssh azureuser@<your_ipaddress>

# Output:
Welcome to Ubuntu 18.04.5 LTS (GNU/Linux 5.4.0-1039-azure x86_64)
azureuser@bennettvm:
```
Now we are logged into the VM we can run various commands for example we can check the resource usage on the machine:

```bash
# Run the top command to see what is running
top
# To quit the top command, press q

# logout by typing exit:
exit

# Output:
logout
Connection to xxx.xxx.xxx.xxx closed.
```

---

## Making a Change In Terraform

Let make a change to our Terraform config and see what happens when we re-apply.

In main.tf find the resource called "myterraformgroup"

Amend the tags section to include a new tag (of your choosing)

**Before**:
```hcl
resource "azurerm_resource_group" "myterraformgroup" {
  name     = "rg-academytf-${var.team_name}-${var.user_id}"
  location = "uksouth"

  tags = {
    project    = "academy"
    team       = var.team_name
    user       = var.user_id
  }
}
```

**After**:
```hcl
resource "azurerm_resource_group" "myterraformgroup" {
  name     = "rg-academytf-${var.team_name}-${var.user_id}"
  location = "uksouth"

  tags = {
    project    = "academy"
    team       = var.team_name
    user       = var.user_id
    mynewtag   = "hello"
  }
}
```

Save your changes.

Now we have made a change to the tags on the resource group. Lets run our Plan again.

```bash
terraform plan
```

**Example Output**:

```bash
  # azurerm_resource_group.myterraformgroup will be updated in-place
  ~ resource "azurerm_resource_group" "myterraformgroup" {
        id       = "/subscriptions/993d9215-8911-4938-8121-9608c3ac1fd5/resourceGroups/rg-academytf-jamez-bennett"
        name     = "rg-academytf-jamez-bennett"
      ~ tags     = {
          + "mynewtag" = "mynewtagvalue"
            # (3 unchanged elements hidden)
        }
        # (1 unchanged attribute hidden)
    }

Plan: 0 to add, 1 to change, 0 to destroy.
```

NB. Sometimes Terraform can update a resource in-place. Some changes require it to destroy and recreate the resource. As you can imagine, this can be disruptive in a real environment. So care is always taken to review these plans carefully before applying.

If you are happy with your change let's apply it:
```bash
terraform apply

# If you are happy answer yes to make the change when prompted
```

You can check the new Tag appears in the portal by viewing your resource group.

---

## Destroying Our Infrastructure

The final command we are going to learn is `destroy` as the name suggests this will delete all the resources deployed by your Terraform config. This is useful when you are creating temporary infrastructure and want to clean up after yourself.

Lets clean up after ourselves:

```bash
terraform destroy

# Review the changes and confirm the prompt as before
```

Confirm your resources are gone in Azure.

---

## Looking deeper at `main.tf`

1. Variables

The variables are defined directly within the main.tf file to ensure the configuration is self-contained. You can also create a `vars.tf` file to store your variables in to provide re-use when you have more complex terraform, but for now we are keeping it simple.

```hcl
variable "team_name" {
  description = "Name of the team"
  type        = string
  default     = "myteam"
}

variable "user_id" {
  description = "User ID"
  type        = string
  default     = "user123"
}
```

These variables will be used throughout the configuration to ensure resource names are unique.

2. Provider Block

```hcl
provider "azurerm" {
  features {}
}
```

A provider in Terraform is a plugin that enables communication with external APIs and services.  It lets Terraform interact with various cloud platforms, such as AWS, infrastructure providers, and other third-party services. Providers are specified within the Terraform configuration code, allowing users to define the specific services and resources they need to manage and provision.

This block specifies the Azure provider, which is necessary for Terraform to interact with Azure. If you were using AWS this would be `aws`.

3. Resource Group

```hcl
resource "azurerm_resource_group" "myterraformgroup" {
  name     = "rg-academytf-${var.team_name}-${var.user_id}"
  location = "uksouth"

  tags = {
    project    = "academy"
    team       = var.team_name
    user       = var.user_id
    mynewtag   = "mynewtagvalue"
  }
}

```

A resource group is a container that holds related resources for an Azure solution, as we have seen before. When you delete a resource group, all associated resources inside it are removed too, making it a handy way to manage your applications.

4. Virtual Network

Defines a virtual network within the resource group. Using the portal this was all done for us and we didn't even know it was being done.

It's similar to a traditional network in your on-premises data center but allows Azure resources, such as Virtual Machines (VMs), to securely communicate with each other, the internet, and on-premises networks.

```hcl
resource "azurerm_virtual_network" "myterraformnetwork" {
  name                = "vnet-academytf-${var.team_name}-${var.user_id}"
  address_space       = ["10.0.0.0/16"]
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}

```

5. Subnet

Subnets help segment the VNet into smaller, more manageable sections. Each subnet can host resources such as VMs, databases, and other services, and each subnet must reside entirely within one VNet.

```hcl
resource "azurerm_subnet" "myterraformsubnet" {
  name                 = "snet-academytf-${var.team_name}-${var.user_id}"
  resource_group_name  = azurerm_resource_group.myterraformgroup.name
  virtual_network_name = azurerm_virtual_network.myterraformnetwork.name
  address_prefixes     = ["10.0.1.0/24"]
}

```

6. Public IP

Allocates a dynamic public IP address so we can access the resource

```hcl
resource "azurerm_public_ip" "myterraformpublicip" {
  name                = "pip-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name
  allocation_method   = "Dynamic"

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}
```

7. Network Security Group

Creates a network security group with rules for SSH and HTTP. A Network Security Group (NSG) in Azure is a security tool that acts as a virtual firewall, controlling inbound and outbound traffic to Azure resources. It contains a list of security rules that allow or deny network traffic based on various criteria such as source and destination IP addresses, ports, and protocols.

```hcl
resource "azurerm_network_security_group" "myterraformnsg" {
  name                = "nsg-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
  security_rule {
    name                       = "HTTP"
    priority                   = 1002
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "80"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}
```

This NSg essentially means only traffic on port 80 (HTTP requests) and 22 (SSH requests) are allowed. All other requests on other ports will be rejected.

8. Network Interface

Creates a network interface and attaches it to the public IP and subnet.

This association means that any inbound traffic to the VM must comply with the rules defined in the NSG, providing an added layer of security.

```hcl
resource "azurerm_network_interface" "myterraformnic" {
  name                = "nic-academytf-${var.team_name}-${var.user_id}"
  location            = "uksouth"
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  ip_configuration {
    name                          = "myNicConfiguration"
    subnet_id                     = azurerm_subnet.myterraformsubnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.myterraformpublicip.id
  }

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}
```

9. Network Interface Security Group Association

Associates the network security group with the network interface.

```hcl
resource "azurerm_network_interface_security_group_association" "example" {
  network_interface_id      = azurerm_network_interface.myterraformnic.id
  network_security_group_id = azurerm_network_security_group.myterraformnsg.id
}
```

10. Virtual Machine

Creates the Linux virtual machine with a custom initialization script to install and run nginx.

```hcl
resource "azurerm_linux_virtual_machine" "myterraformvm" {
  name                  = "vm-academytf-${var.team_name}-${var.user_id}"
  location              = "uksouth"
  resource_group_name   = azurerm_resource_group.myterraformgroup.name
  network_interface_ids = [azurerm_network_interface.myterraformnic.id]
  size                  = "Standard_B1ls"

  os_disk {
    name                 = "myOsDisk"
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "18.04-LTS"
    version   = "latest"
  }

  computer_name                   = "${var.user_id}vm"
  admin_username                  = "azureuser"
  disable_password_authentication = true

  admin_ssh_key {
    username   = "azureuser"
    public_key = file("~/.ssh/id_rsa.pub")
  }

  boot_diagnostics {}

  custom_data = base64encode(<<-EOT
    #!/usr/bin/env bash
    sudo apt-get update -y
    sudo apt-get install nginx -y
    sudo systemctl restart nginx
  EOT
  )

  tags = {
    project = "academy"
    team    = var.team_name
    user    = var.user_id
  }
}
```

11. Public IP Data Source

Fetches the dynamically allocated public IP address after the VM is created. IP addresses can be either statically or dynamically allocated; dynamic public IP addresses are sufficient and more cost-effective. They are commonly used for non-critical services or testing environments where the IP address does not need to remain the same. 

Once the VM is created and running, you'll need to know its public IP address to connect to it, especially for remote management or accessing services running on the VM.

```hcl
data "azurerm_public_ip" "public_ip" {
  name                = "pip-academytf-${var.team_name}-${var.user_id}"
  resource_group_name = azurerm_resource_group.myterraformgroup.name
  depends_on          = [azurerm_public_ip.myterraformpublicip, azurerm_linux_virtual_machine.myterraformvm]
}
```

12. Output

Outputs the public IP address of the VM so we can access it.

```hcl
output "azurerm_public_ip" {
  value       = data.azurerm_public_ip.public_ip.ip_address
  description = "Public IP associated with the VM instance"
}
```